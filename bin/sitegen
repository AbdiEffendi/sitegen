#!/usr/bin/env moon

require "moonscript"
require "moon"
require "cosmo"
require "date"

require "sitegen.common"

action = arg[1] or "build"

default = {
  sitefile: "site.moon"
  files:
    page: [==[
    create_date: $eval{"date()"}
    $if{"title"}[[title: $title]]
--

]==]
    sitefile: [==[
require "sitegen"
site = sitegen.create_site =>
  @title = $title
site\write!
]==]
}

log = (...) ->
  print "->", ...

scope = (t={}) ->
  moon.extend t, {
    eval: (arg) ->
      code = "return -> " .. arg[1]
      moonscript.loadstring(code)!!
    if: (arg) ->
      var_name = arg[1]
      cosmo.yield t if t[var_name]
      nil
  }

get_site = ->
  if Path.exists default.sitefile
    true
  else
    false

annotate = (obj, verbs) ->
  setmetatable {}, {
    __newindex: (name, value) =>
      obj[name] = value
    __index: (name) =>
      fn =  obj[name]
      return fn if not type(fn) == "function"
      if verbs[name]
        (...) ->
          fn ...
          first = ...
          log verbs[name], first
      else
        fn
  }

Path = annotate Path, {
  mkdir: "made directory"
  write_file: "wrote"
}

columnize = (rows, indent=2, padding=4) ->
  max = 0
  max = math.max max, #row[1] for row in *rows

  formatted = for row in *rows
    padd = (max - #row[1]) + padding
    table.concat {
      (" ")\rep indent
      row[1]
      (" ")\rep padd
      row[2]
    }

  table.concat formatted, "\n"

tasks = {
  new: ->
    if get_site!
      error "sitefile already exists: " .. default.sitefile

    title = ("%q")\format arg[2] or "Hello World"

    Path.mkdir"www"
    Path.mkdir"templates"

    site_moon = cosmo.f(default.files.sitefile) scope{:title}
    Path.write_file default.sitefile, site_moon

  page: ->
    error "missing sitefile" if not get_site!
    path, title = arg[2], arg[3]

    if not title
      title = path
      path_part, title_part = title\match"^(.-)([^/]+)$"
      if path_part
        title = title_part
        path = path_part
      else
        path = '.'

    Path.mkdir path if Path.normalize(path) != ""

    -- iterater for all potential file names
    names = (fname, ext=".md") ->
      i = 0
      coroutine.wrap ->
        while true
          coroutine.yield if i == 0
            fname .. ext
          else
            table.concat {fname, "_", i, ext }
          i += 1

    full_path = nil
    for name in names slugify title
      full_path = Path.join path, name
      if not Path.exists full_path
        break

    Path.write_file full_path, cosmo.f(default.files.page) scope{:title}

  deploy: ->
    get_site! or error "no site"
    host, path = arg[2], arg[3]
    error "need host" if not host
    error "need path" if not path

    deploy = require "sitegen.deploy"

    log "uploading..."
    with deploy.Sync host, path
      \upload!

  build: ->
    file = io.open default.sitefile
    error "missing site file: " .. default.sitefile if not file
    file\close!
    moonscript.loadfile(default.sitefile)!

  help: ->
    print "Sitegen"
    print "usage: sitegen <action> [args]"
    print!
    print "Available actions:"

    print!
    print columnize {
      {"new", "Create a new site in the current directory"}
      {"build", "Build (or rebuild) all pages"}
      {"page <path> [title]", "Create a new markdown page at path"}
      {"deploy <host> <path>", "Deploy site to host over ssy (rsync)"}
    }
    print!
}

-- potential commands:
-- plugins: sitegen blog:post "Hello World"

if not tasks[action]
  error "unknown task: " .. action

tasks[action]!
